description = 'ELKI - Main Module'
dependencies {
  compile project(':elki-logging')
  compile project(':elki-core-api')
  compile project(':elki-core-data')
  compile project(':elki-core-distance')
  compile project(':elki-core-math')
  compile project(':elki-core-util')
  compile project(':elki-core-dbids')
  compile project(':elki-core-parallel')
  compile project(':elki-input')
  compile project(':elki-persistent')
  compile project(':elki-classification')
  compile project(':elki-index')
  compile project(':elki-index-rtree')
  compile project(':elki-index-mtree')
  compile group: 'net.sf.trove4j', name: 'trove4j', version:'[3.0.3,)'
  runtime project(':elki-core-dbids-int')
  testCompile project(':elki-docutil')
  testCompile group: 'junit', name: 'junit', version:'[4.8,)'
}

jar {
  manifest {
    // Expand ${project.version} in MANIFEST.MF
    from("src/main/resources/META-INF/MANIFEST.MF") {
      eachEntry { it.value = it.value.replace('${project.version}', project.version) }
    }
    attributes(
      "Main-Class" : "de.lmu.ifi.dbs.elki.application.ELKILauncher",
      "Class-Path": configurations.runtime.collect { "lib/"+it.getName() }.join(' ')
    )
  }
}

// Resources
processResources {
  filesMatching('**/elki.properties') {
    expand('project': project)
  }
}
javadoc.dependsOn documentParameters
javadoc.dependsOn documentReferences

